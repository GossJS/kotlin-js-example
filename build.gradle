group 'com.edutron'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.0-rc-91'
    ext.selenium_version = '2.52.0'
    ext.web_dir = 'web'
    ext.test_dir = 'test'
    repositories {
        mavenCentral()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
    test.kotlin.srcDirs += "src/test/kotlin"
}

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

clean.doFirst() {
    delete("${web_dir}")
    delete("${test_dir}")
}

build.doLast() {
    // Copy kotlin.js and kotlin-meta.js from jar into web directory
    // TODO Replace with CDN link in html when available.
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/${web_dir}"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }

    // Unpack built jar into web directory
    File artifact = new File("build/libs/${project.name}-${project.version}.jar")
    copy {
        includeEmptyDirs = false

        from zipTree(artifact)
        into "${web_dir}"
        include { fileTreeElement ->
            def path = fileTreeElement.path
            !path.startsWith("META-INF/")
        }
    }
    
    // Copy test scripts
    copy {
        includeEmptyDirs = false
        from new File("build/classes/test")
        into "${test_dir}"
    }

    // Copy test resources
    copy {
        includeEmptyDirs = false
        from new File("build/resources/test")
        into "${test_dir}"
    }

}
